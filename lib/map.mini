import core;

class KeyNotFound extends Exception {}

class Map[K, V] extends IndexableCollection[K, V, Pair[K, V]] {
    
    abstract def get(key : K) -> V? {}
    abstract def insert(key : K, value : V) {}
    abstract def remove(key : K) -> V? {}

    def indices() -> Collection[K] {
        return self.keys();
    }
    def keys() -> Collection[K] {
        return MapKeys{self};
    }
    def values() -> Collection[V] {
        return MapValues{self};
    }
    def clear() {
        for key in self.keys() { self.remove(key); }
    }
    def [](key : K) -> V {
        val = self.get(key);
        if val is V { return val; }
        yield(KeyNotFound{});
    }
    def []=(key : K, value : V) {
        self.insert(key, value);
    }
}

class MapKeys[K, V] extends Collection[K] {
    @map : Map[K, V]

    def init(@map : Map[K, V]) {
    }
    def size() -> i32 {
        return @map.size();
    }
    def iterator() -> Iterator[K] {
        return MapKeyIterator{@map.iterator()};
    }
}

class MapValues[K, V] extends Collection[V] {
    @map : Map[K, V]

    def init(@map : Map[K, V]) {
    }
    def size() -> i32 {
        return @map.size();
    }
    def iterator() -> Iterator[V] {
        return MapValueIterator{@map.iterator()};
    }
}

class MapKeyIterator[K, V] extends Iterator[K] {
    @map_iterator : Iterator[Pair[K, V]]

    def init(@map_iterator : Iterator[Pair[K, V]]) {}

    def next() -> K? {
        pair = @map_iterator.next();
        if pair is Pair[K, V] { return pair.first(); }
        return nil;
    }
}

class MapValueIterator[K, V] extends Iterator[V] {
    @map_iterator : Iterator[Pair[K, V]]

    def init(@map_iterator : Iterator[Pair[K, V]]) {}

    def next() -> V? {
        pair = @map_iterator.next();
        if pair is Pair[K, V] { return pair.second(); }
        return nil;
    }
}

class HashMap[K, V] extends Map[K, V] {
    // You can construct a HashMap with a hash function and an equality function
    abstract def init(hasher: (K) -> i32, eq : (K, K) -> Bool) {}
}

def i32_hasher_old(i : i32) -> i32 {
    i = (i + 0x7ed55d16) + (i << 12);
    i = (i bit_xor 0xc761c23c) bit_xor (i >> 19);
    i = (i + 0x165667b1) + (i << 5);
    i = (i + 0xd3a2646c) bit_xor (i << 9);
    i = (i + 0xfd7046c5) + (i << 3);
    i = (i bit_xor 0xb55a4f09) bit_xor (i >> 16);
    return i;
}

def i32_hasher(x: i32) -> i32 {
    return i32_hasher_lowbias(x);
}

def i32_hasher_murmur(x: i32) -> i32 {
    x = x bit_xor (x >> 16);
    x = x * 0x85ebca6b;
    x = x bit_xor (x >> 13);
    x = x * 0xc2b2ae35;
    x = x bit_xor (x >> 16);
    return x;
}

def i32_hasher_lowbias(x: i32) -> i32 {
    x = x bit_xor (x >> 16);
    x = x * 0x7feb352d;
    x = x bit_xor (x >> 15);
    x = x * 0x846ca68b;
    x = x bit_xor (x >> 16);
    return x;
}

def i32_hasher_weak(i: i32) -> i32 {
    return i bit_xor (i << 2);
}

def i32_hasher_identity(i: i32) -> i32 {
    return i;
}

def i32_hasher_low_bits(i: i32) -> i32 {
    return i bit_and 0xFFFF;  // Only 16 bits of entropy
}

def i32_hasher_simd_friendly(i: i32) -> i32 {
    mixed = i * -1640531535;
    
    // Slot: 14 bits → 16K unique slots → ~64 keys/slot collision
    slot = (mixed >> 6) bit_and 0x3FFF;
    
    // H2: from different bits → diverse within collision chain
    h2 = mixed bit_and 0x3F;
    
    // Pack: H2 in bits 26-31, slot in bits 0-13
    return (h2 << 26) bit_or slot;
}

def i32_eq(a : i32, b : i32) -> Bool {
    return a == b;
}

def string_hasher(s : String) -> i32 {
    hash : i32 = 0;
    seed : i32 = 31;
    for c in s {
        hash = (hash * seed + c.codepoint()) bit_and 0x7FFFFFFF;
    }
    return hash;
}

def string_eq(a : String, b : String) -> Bool {
    return a == b;
}
