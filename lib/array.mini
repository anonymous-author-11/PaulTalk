import core;

class Array[T] extends
    List[T],
    IndexableCollection[i32, T, T],
    Representable
{
	@buffer : Buffer[T]
	@length : i32
	@capacity : i32

    regions { @elems_reg == @buffer.elems_reg }

    def Self.empty() -> Self { return Self{}; }

    // When iterable is actually a Collection, the optimized dispatch for Array.extend(collection) will be used
    // A great example of why multimethods are so powerful: always dispatches on argument's runtime type
    def Self.collect(iterable : Iterable[T]) -> Self {
        return Self.empty().extend(iterable);
    }

	def init() {
		@buffer = Buffer[T].new(1);
		@length = 0;
		@capacity = 1;
	}
    def init(@capacity : i32) {
        @buffer = Buffer[T].new(@capacity);
        @length = 0;
    }
    def init(@buffer : Buffer[T], @length : i32, @capacity : i32) {
    }
    #[ret == @length]
    def size() -> i32 {
        return @length;
    }
    #[ret == @capacity]
    def capacity() -> i32 {
    	return @capacity;
    }
    def reserve(new_capacity : i32) {
        if new_capacity <= @capacity { return; }
        @capacity = new_capacity;
        old_buffer = @buffer;
        @buffer = Buffer[T].new(@capacity);
        i = 0;
        while i < @length {
            @buffer.[i] = old_buffer.[i];
            i = i + 1;
        }
    }

    // List[T] overrides

    #[ret == self, x == @elems_reg]
    def append(x : T) -> Self {
        if @length >= @capacity { self.reserve(@capacity * 2); }
        @buffer.[@length] = x;
        @length = @length + 1;
        return self;
    }
    #[ret == self, other.elems_reg == @elems_reg]
    def extend(other : Collection[T]) -> Self {
        old_len = @length;
        new_len = old_len + other.size();
        self.reserve(new_len);
        i = 0;
        for elem in other {
            self.unchecked_insert(i + old_len, elem);
            i = i + 1;
        }
        @length = new_len;
        return self;
    }

    // IndexableCollection[i32, T, T] overrides

    def indices() -> Range {
        return 0..@length;
    }
    def values() -> Self {
        return self;
    }
    #[ret == @elems_reg]
    def [](index : i32) -> T yields OutOfBounds {
        self.bounds_check(index);
        if index < 0 { index = @length + index; }
        return self.unchecked_index(index);
    }
    #[value == @elems_reg]
    def []=(index : i32, value : T) yields OutOfBounds {
        self.bounds_check(index);
        if index < 0 { index = @length + index; }
        self.unchecked_insert(index, value);
    }

    def bounds_check(index : i32) yields OutOfBounds {
        past_end = index > (@length - 1);
        before_start = (@length + index) < 0;
        if past_end or before_start {
            yield(OutOfBounds.new(@length, index));
        }
    }
    #[ret == @elems_reg]
    def unchecked_index(index : i32) -> T {
    	return @buffer.[index];
    }
    #[value == @elems_reg]
    def unchecked_insert(index : i32, value : T) {
        @buffer.[index] = value;
    }
    def iterator() -> ConstantTimeIterator[T] {
    	return ArrayIterator{self};
    }
    def repr() -> String {
        return "Array";
    }
}

class ArrayIterator[T] extends ConstantTimeIterator[T] {
	@array : Array[T]
	@index : i32

    regions { @elems_reg == @array.elems_reg }

	def init(@array : Array[T]) { @index = 0; }
	def next() -> T | Nil {
        // We check the array's current .length() on every .next() call
        // This guarantees no out-of-bounds access if the array was modified
		if @index >= @array.length() { return nil; }
        old_index = @index;
		@index = @index + 1;
		return @array.unchecked_index(old_index);
	}
}