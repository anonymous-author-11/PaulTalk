import builtins;

class Pair[T, U] {
	@first : T
	@second : U

    getters @first, @second
    setters @first, @second

	def init(@first : T, @second : U) {
    }

}

class Iterator[T] {
    @elems_reg : Region
    @nxt : Region

    regions { @nxt holds @elems_reg }

	abstract def next() -> T? {
    } ~> { ret == @nxt }

}

class Iterable[T] {
    @elems_reg : Region

	abstract def iterator() -> Iterator[T] {
    } ~> { ret.elems_reg == @elems_reg, ret holds self }

	def each(f : (T)) {
        for x in self { f.call(x); }
    } ~> no_alias
    
    def reduce(accumulator : T, f : (T, T) -> T) -> T {
        for x in self { accumulator = f.call(accumulator, x); }
        return accumulator;
    } ~> { ret == accumulator }

    def all(f : (T) -> Bool) -> Bool {
        for x in self { if not f.call(x) { return false; } }
        return true;
    } ~> no_alias

    def any(f : (T) -> Bool) -> Bool {
        for x in self { if f.call(x) { return true; } }
        return false;
    } ~> no_alias

    def first() -> T? {
        for x in self { return x; }
        return nil;
    }

    def last() -> T? {
        last_value = nil;
        for value in self { last_value = value; }
        return last_value;
    }

    def find(f : (T) -> Bool) -> T? {
        for x in self { if f.call(x) { return x; } }
        return nil;
    }
    
    def enumerate() -> Iterable[Pair[i32, T]] {
        return Enumerated{self};
    } ~> { ret holds self, ret.elems_reg == @elems_reg }

    def map[U](f : (T) -> U) -> Iterable[U] {
        return Mapped{self, f};
    } ~> { ret holds (self, f), ret.elems_reg == @elems_reg }
    
    def filter(f : (T) -> Bool) -> Iterable[T] {
        return Filtered{self, f};
    } ~> { ret holds (self, f), ret.elems_reg == @elems_reg }
    
    def chain(other : Iterable[T]) -> Iterable[T] {
        return Chained{self, other};
    } ~> {
        ret holds (self, other),
        ret.elems_reg holds (@elems_reg, other.elems_reg)
    }
    
    def interleave(other : Iterable[T]) -> Iterable[T] {
        return Interleaved{self, other};
    } ~> {
        ret holds (self, other),
        ret.elems_reg holds (@elems_reg, other.elems_reg)
    }
    
    def zip[U](other : Iterable[U]) -> Iterable[Pair[T, U]] {
        return Zipped{self, other};
    } ~> {
        ret holds (self, other),
        ret.elems_reg holds (@elems_reg, other.elems_reg)
    }
    
    def product[U](other : Iterable[U]) -> Iterable[Pair[T, U]] {
        return Products{self, other};
    } ~> {
        ret holds (self, other),
        ret.elems_reg holds (@elems_reg, other.elems_reg)
    }
}

// Must be O(1) to get next element
// ( O(1) with respect to what? )
class ConstantTimeIterator[T] extends Iterator[T] {}

// Adapts a Coroutine into an Iterator
// An alternative to writing boilerplate Iterator classes for your types
// Generally less efficient than writing the classes
class CoroIterator[T] extends Iterator[T] {
    @coro : Coroutine[() yields T?]

    def init(@coro : Coroutine[() yields T?]) {}

    def next() -> T? { return @coro.call(); }
}

class Enumerated[T] extends Iterable[Pair[i32, T]] {
    @iterable : Iterable[T]

    regions { @elems_reg == @iterable.elems_reg }

    def init(@iterable : Iterable[T]) {}

    def iterator() -> Enumerator[T] {
        return Enumerator{@iterable.iterator()};
    } ~> { ret.iterator holds @iterable }
}

class Enumerator[T] extends Iterator[Pair[i32, T]] {
    @iterator : Iterator[T]
    @index : i32

    regions {
        @elems_reg == @iterator.elems_reg,
        @nxt.second == @iterator.nxt
    }

    def init(@iterator : Iterator[T]) {
        @index = 0;
    }

    def next() -> Pair[i32, T]? {
        next = @iterator.next();
        if next is T {
            old_index = @index;
            @index = @index + 1;
            return Pair{old_index, next};
        }
        return nil;
    }
}

class Mapped[T, U] extends Iterable[U] {
    @iterable : Iterable[T]
    @f : (T) -> U

    regions { @elems_reg == @iterable.elems_reg }

    def init(@iterable : Iterable[T], @f : (T) -> U) {}

    def iterator() -> Mapper[T, U] {
        return Mapper{@iterable.iterator(), @f};
    } ~> { ret.f == @f, ret.iterator holds @iterable }
} 

class Mapper[T, U] extends Iterator[U] {
    @iterator : Iterator[T]
    @f : (T) -> U

    regions { @elems_reg == @iterator.elems_reg }

    def init(@iterator : Iterator[T], @f : (T) -> U) {}

    def next() -> U? {
        next = @iterator.next();
        if next is T { return @f.call(next); }
        return nil;
    }
}

class Filtered[T] extends Iterable[T] {
    @iterable : Iterable[T]
    @f : (T) -> Bool

    regions { @elems_reg == @iterable.elems_reg }

    def init(@iterable : Iterable[T], @f : (T) -> Bool) {}

    def iterator() -> Filterer[T] {
        return Filterer{@iterable.iterator(), @f};
    } ~> { ret.f == @f, ret.iterator holds @iterable }
}

class Filterer[T] extends Iterator[T] {
    @iterator : Iterator[T]
    @f : (T) -> Bool

    regions {
        @elems_reg == @iterator.elems_reg,
        @nxt == @iterator.nxt
    }
    
    def init(@iterator : Iterator[T], @f : (T) -> Bool) {}

    def next() -> T? {
        next = @iterator.next();
        while next is T {
            if @f.call(next) { return next; }
            next = @iterator.next();
        }
        return nil;
    }
}

class Chained[T] extends Iterable[T] {
    @first : Iterable[T]
    @second : Iterable[T]

    regions { @elems_reg holds (@first.elems_reg, @second.elems_reg) }

    def init(@first : Iterable[T], @second : Iterable[T]) {}

    def iterator() -> Chainer[T] {
        return Chainer{@first.iterator(), @second.iterator()};
    } ~> {
        ret.second.elems_reg == @second.elems_reg,
        ret.first.elems_reg == @first.elems_reg,
        ret.second holds @second,
        ret.first holds @first
    }

}

class Chainer[T] extends Iterator[T] {
    @first : Iterator[T]
    @second : Iterator[T]
    @on_first : Bool

    regions {
        @elems_reg holds (@first.elems_reg, @second.elems_reg),
        @nxt == @first.nxt == @second.nxt
    }

    def init(@first : Iterator[T], @second : Iterator[T]) {
        @on_first = true;
    }
    def next() -> T? {
        if @on_first { 
            next = @first.next();
            if next is T { return next; }
            @on_first = false;
        }
        return @second.next();
    }
}

class Interleaved[T] extends Iterable[T] {
    @first : Iterable[T]
    @second : Iterable[T]

    regions { @elems_reg holds (@first.elems_reg, @second.elems_reg) }

    def init(@first : Iterable[T], @second : Iterable[T]) {}

    def iterator() -> Interleaver[T] {
        return Interleaver{@first.iterator(), @second.iterator()};
    } ~> {
        ret.second.elems_reg == @second.elems_reg,
        ret.first.elems_reg == @first.elems_reg,
        ret.second holds @second,
        ret.first holds @first
    }

}

class Interleaver[T] extends Iterator[T] {
    @first : Iterator[T]
    @second : Iterator[T]
    @on_first : Bool

    regions {
        @elems_reg holds (@first.elems_reg, @second.elems_reg),
        @nxt == @first.nxt == @second.nxt
    }

    def init(@first : Iterator[T], @second : Iterator[T]) {
        @on_first = true;
    }
    def next() -> T? {
        if @on_first {
            @on_first = false;
            nxt = @first.next();
            if nxt is T { return nxt; }
            return @second.next();
        }
        @on_first = true;
        nxt = @second.next();
        if nxt is T { return nxt; }
        return @first.next();
    }
}

class Zipped[T, U] extends Iterable[Pair[T, U]] {
    @first : Iterable[T]
    @second : Iterable[U]

    regions { @elems_reg holds (@first.elems_reg, @second.elems_reg) }

    def init(@first : Iterable[T], @second : Iterable[U]) {}

    def iterator() -> Zipper[T, U] {
        return Zipper{@first.iterator(), @second.iterator()};
    } ~> {
        ret.second.elems_reg == @second.elems_reg,
        ret.first.elems_reg == @first.elems_reg,
        ret.second holds @second,
        ret.first holds @first
    }

}

class Zipper[T, U] extends Iterator[Pair[T, U]] {
    @first : Iterator[T]
    @second : Iterator[U]

    regions {
        @elems_reg holds (@first.elems_reg, @second.elems_reg),
        @nxt.first == @first.nxt, @nxt.second == @second.nxt
    }

    def init(@first : Iterator[T], @second : Iterator[U]) {
    }

    def next() -> Pair[T, U]? {
        first_elem = @first.next();
        second_elem = @second.next();
        // notice the sophistication of the flow-typing
        if first_elem is Nil or second_elem is Nil { return nil; }
        return Pair{first_elem, second_elem};
    }

}

class Products[T, U] extends Iterable[Pair[T, U]] {
    @first : Iterable[T]
    @second : Iterable[U]

    regions { @elems_reg holds (@first.elems_reg, @second.elems_reg) }

    def init(@first : Iterable[T], @second : Iterable[U]) {}

    def iterator() -> Productizer[T, U] {
        return Productizer{@first.iterator(), @second};
    } ~> {
        ret.second == @second, 
        ret.first_iterator.elems_reg == @first.elems_reg,
        ret.current_first holds @first.elems_reg,
        ret.first_iterator holds @first
    }

}

class Productizer[T, U] extends Iterator[Pair[T, U]] {
    @first_iterator : Iterator[T]
    @second_iterator : Iterator[U]
    @second : Iterable[U]
    @current_first : T?

    regions {
        @elems_reg holds (@current_first, @second_iterator.elems_reg),
        @first_iterator.nxt == @current_first,
        @second_iterator.elems_reg == @second.elems_reg,
        @second_iterator holds @second,
        @nxt.first == @first_iterator.nxt, @nxt.second == @second_iterator.nxt
    }

    def init(@first_iterator : Iterator[T], @second : Iterable[U]) {
        @second_iterator = @second.iterator();
        @current_first = @first_iterator.next();
    }

    def next() -> Pair[T, U]? {
        current_first = @current_first;
        while current_first is T {
            second_elem = @second_iterator.next();
            if second_elem is U { return Pair{current_first, second_elem}; }
            @current_first = @first_iterator.next();
            @second_iterator = @second.iterator();
            current_first = @current_first;
        }
        return nil;
    }
}
